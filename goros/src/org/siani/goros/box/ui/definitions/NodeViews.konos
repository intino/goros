dsl Konos

use Tasks
use TaskViews
use UI
use Theme

Template nodeLinksInTemplate as Relative(height=100%)
	Table(itemClass="org.monet.space.kernel.model.Node", scrollingMark=false, format=bordered) linksInTable
		noItemsMessage = "No hay elementos que lo relacionen"
		Mold
			Heading linksInTableLabelHeading > Text(value="Título")
			Item(height=50px,width=16%) linksInTableLabelItem > Link(title="Cargando...") label as Action Addressable(addressableResource=empty)
		Mold
			Heading linksInTableDescriptionHeading > Text(value="Descripción")
			Item(height=50px,width=16%) linksInTableDescriptionItem > Text description

Template nodeLinksOutTemplate as Relative(height=100%)
	Table(itemClass="org.monet.space.kernel.model.Node", scrollingMark=false, format=bordered) linksOutTable
		noItemsMessage = "No hay elementos con los que se relacione"
		Mold
			Heading linksOutTableLabelHeading > Text(value="Título")
			Item(height=50px,width=16%) linksOutTableLabelItem > Link(title="Loading...") label as Action Addressable(addressableResource=empty)
		Mold
			Heading linksOutTableDescriptionHeading > Text(value="Descripción")
			Item(height=50px,width=16%) linksOutTableDescriptionItem > Text description

Template nodeRevisionsTemplate as Relative(height=100%)
	Table(itemClass="org.monet.space.kernel.model.Revision", scrollingMark=false, format=bordered) nodeRevisionsTable as Selectable(multiple=false)
		noItemsMessage = "No hay versiones del elemento registradas"
		Mold
			Heading nodeRevisionsTableLabelHeading > Text(value="Título")
			Item(height=50px,width=80%) nodeRevisionsTableLabelItem > Date label > mode = FromNow
		Mold
			Heading nodeRevisionsTableOperationsHeading > Block(layout=Horizontal EndJustified) > Text(value="Operaciones")
			Item(height=50px,width=20%) nodeRevisionsTableOperationsItem
				Block(layout=Horizontal EndJustified)
					MaterialIconButton(title="Restaurar", icon="Restore") restore as Action Affirmed("¿Está seguro que quiere restaurar esta revisión?")

Template(layout=Horizontal) nodeNotesTemplate as Relative(height=100%)

	Block(layout=Vertical Flexible, format=bordered spaced)
		Table(itemClass="org.siani.goros.box.ui.datasources.model.Note", scrollingMark=false, format=bordered) nodeNotesTable
			noItemsMessage = "No se han registrado notas"
			Mold
				Heading nodeNotesTableNameHeading > Text(value="Nombre")
				Item(height=30px,width=30%) nodeNotesTableNameItem > Text name
			Mold
				Heading nodeNotesTableValueHeading > Text(value="Valor")
				Item(height=30px,width=60%) nodeNotesTableValueItem > Text value
			Mold
				Heading nodeNotesTableOperationsHeading
				Item(height=30px,width=10%) nodeNotesTableOperationsItem
					Block(layout=Horizontal EndJustified)
						MaterialIconButton(title="Eliminar nota", icon="Clear", size=Small) remove as Action Affirmed("¿Está seguro que quiere borrar la nota?")

	Block(layout=Vertical, format=bordered spaced smallAirLeft) nodeNotesDialog as Relative(width=20%)
		Text(value="Añadir nota", format=h5 doubleAirBottom)
		Text(format=airRight doubleAirBottom) nameField as Editable Labeled("Nombre")
		Text(format=airRight doubleAirBottom) valueField as Editable Labeled("Valor")
		Block as Absolute(width=100px) > Button(title="Añadir", highlight=Outline) add as Action

Template(layout=Vertical Flexible, format=spaced) nodeLocationTemplate as Relative(height=100%)
	Location location as Editable Readonly
		Zoom(defaultZoom = 15, min = 1, max = 18)
		Center(latitude = 28.146773457066104, longitude = -15.418557420532238)

Template(format=spaced relative) nodeDocumentTemplate as Relative(height=100%)
	MaterialIconButton(title="Modify document", visible=false, icon="Edit", format=editDocumentButtonStyle) edit as OpenDialog(editDocumentDialog)
	File preview

	Dialog(title="Modify document") editDocumentDialog as Relative(width=60%, height=60%)
		Block as Relative(height=100%, offsetHeight=50px)
			Stepper(position=Top) stepper
				Step downloadStep as Labeled("Download document")
					Block(layout=Vertical CenterCenter) as Relative(height=100%)
						Text(value="Download document to edit it in your computer.", translate=true)
						Text(value="Click next if you have already edit the document.", translate=true, format=airBottom)
						Block > Button(title="Download", highlight=Outline) downloadDocument as Download
				Step editingStep as Labeled("Edit document")
					Block(layout=Vertical CenterCenter) as Relative(height=100%)
						Text(value="Open the document and insert the changes that you want.", translate=true)
						Text(value="When you finish, indicate the location of the modified document to save the last version in the system.", translate=true, format=airBottom)
						Block > File(showDropZone=true) newDocument as Editable
				Step finishStep as Labeled("Document saved")
					Block(layout=Vertical CenterCenter) as Relative(height=100%)
						Text(value="New version of document received.", translate=true)
						Text(value="Click save to replace current version.", translate=true)
		Divider
		Block(layout=Horizontal EndJustified)
			Button(title="Cancel", format=airRight) as CloseDialog(editDocumentDialog)
			Button(title="Save", highlight=Fill) save as Action

Template(format=nodeFieldStyle) fieldNodeTemplate
	Text(format=h5 airBottom) title
	Block(layout=Horizontal CenterJustified) as Relative(height=100%)
		Block as Relative(width=100%)
			Block(visible=false) noTypesDefinedBlock
				Text(value="Your model is wrong. Property 'add' or 'contain' not defined.", translate=true, format=red)
			Block(visible=false) emptyBlock
				Text(value="No element defined", translate=true)
				Block(format=airTop) as Absolute(width=175px)
					Button(title="Add element", highlight=Outline, visible=false) addType as Action
					Button(title="Add element", highlight=Outline, visible=false) selectType as OpenDialog(selectNodeTypeDialog)
			DisplayStamp(display=empty) nodeFrame

	DecisionDialog(title="Select element type") selectNodeTypeDialog
		Selector nodeTypeSeletor as ListBox
