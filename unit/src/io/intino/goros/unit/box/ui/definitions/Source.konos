dsl Konos

use Theme

Template(layout=Vertical Flexible, format=smallAir) sourceTemplate
    Block(layout=Horizontal, format=bordered spaced) header
        Block(layout=Vertical Flexible)
            Block(layout=Horizontal Center)
                Link(title="", format=h4 middleAirRight, visible=false) desktopLink as Action Addressable(empty)
                Text(value="/", format=h4 middleAirRight, visible=false) desktopLinkSeparator
                Text(format=h4 bold) label
            Text(format=body2) count
            TemplateStamp(template=sourceLegendTemplate)

    Block(layout=Horizontal Flexible, format=smallAirTop)
        Block(layout=Vertical Flexible, format=bordered)
            TemplateStamp(template=sourceEmbeddedTemplate) content

Template(layout=Horizontal Flexible) sourceEmbeddedTemplate as Relative(height=100%)
	Block(layout=Vertical Flexible, format=autoOverflow) levelsBlock as Relative(width=100%) > TemplateStamp(template=sourceLevelTemplate) levels as Multiple(arrangement=Horizontal, wrapItems=false)
	Block(format=smallAirLeft leftBordered) termView as Conditional Animated Relative(width=35%)
		Transition(direction=Left)
	 	//Block(layout=Vertical Flexible)
		TemplateStamp(template=publishTermsDialog) publishTermsDialog
		TemplateStamp(template=sourceTermDialog) termDialog

Template(layout=Vertical, format=rightBordered sourceLevelWidth) sourceLevelTemplate as Relative(height=100%)
	List(itemClass="org.monet.space.kernel.model.Term", scrollingMark=false) sourceLevelList as Selectable(multiple=false)
		noItemsMessage = "No existen tÃ©rminos"
		Mold
			Item(height=90px) sourceLevelListItem
				Block(layout=Horizontal, format=termReferenceStyle relative)
					Block(format=termDisabledStyle, visible=false) disabledLayer
					Block(layout=Vertical Flexible)
						Block(layout=Horizontal Center)
							Text(format=body2 legendPillStyle middleAirRight) type as Highlighted(textColor="white", backgroundColor="transparent")
							Text(format=middleAirRight, cropWithEllipsis=20Chars) label
						Text(format=body2 grey, cropWithEllipsis=26Chars) code
						Number(format=body2) tagsCount as Labeled("Tags count")
					Block
						MaterialIcon(icon="KeyboardArrowRight", visible=false) groupFlag

Template(layout=Horizontal Center) sourceLegendTemplate
	Block(layout=Horizontal Center, format=doubleAirRight) > Text(value="C", format=body2 legendPillStyle middleAirRight) as Highlighted(textColor="white", backgroundColor="#4CAF50"); Text(value="Category", format=body2, translate=true)
	Block(layout=Horizontal Center, format=doubleAirRight) > Text(value="S", format=body2 legendPillStyle middleAirRight) as Highlighted(textColor="white", backgroundColor="#2896F3"); Text(value="Selectable", format=body2, translate=true)
	Block(layout=Horizontal Center, format=doubleAirRight) > Text(value="T", format=body2 legendPillStyle middleAirRight) as Highlighted(textColor="white", backgroundColor="#272C34"); Text(value="Term", format=body2, translate=true)

Template publishTermsDialog
	Block(format=doubleAirBottom middleSpaced leftSpaced rightSpaced)
		Text(value="New terms added recently that are not published yet", format=h5 airBottom, translate=true)
		Block(format=bordered dialog)
			Selector newTerms as CheckBox(layout=Horizontal)
			Block(layout=Horizontal)
				Block(format=airRight) as Absolute(width=120px) > Button(title="Publish", highlight=Outline, size=Small) publish as Action Readonly
				Block(format=airRight) as Absolute(width=120px) > Button(title="Publish all", highlight=Outline, size=Small) publishAll as Action
				Block as Absolute(width=120px) > Button(title="Delete", highlight=Outline, size=Small) delete as Action Readonly

Template sourceTermDialog
	Block(format=doubleAirBottom middleSpaced leftSpaced rightSpaced) generalView
		Text(format=h5 airBottom) title
		Block(format=bordered dialog)
			Text(format=doubleAirBottom) code as Editable Readonly Labeled("Code")
			Text(format=doubleAirBottom) label as Editable Labeled("Label")
			Selector type as ComboBox(placeholder="Select type") Labeled("Type")
				Text(value="Term", translate=true) termType as Option
				Text(value="Category", translate=true) categoryType as Option
			Switch(title="Contain other terms", format=doubleAirBottom) containOtherTerms as Action
			Text(value="Tags", translate=true, format=grey)
			TemplateStamp(template=sourceTermTagTemplate) tags as Multiple(noItemsMessage="No tags")
			Block(layout=Horizontal, format=airTop airBottom)
				Block(format=airRight, visible=false) enableBlock as Absolute(width=120px) > Button(title="Enable", highlight=Outline, size=Small) enable as Action
				Block(format=airRight, visible=false) disableBlock as Absolute(width=120px) > Button(title="Disable", highlight=Outline, size=Small) disable as Action
				Block(format=airRight, visible=false) deleteBlock as Absolute(width=120px) > Button(title="Delete", highlight=Outline, size=Small) delete as Action

	Block(format=middleSpaced leftSpaced rightSpaced) childrenView
		Text(format=h5 airBottom) addTitle
		Block(format=bordered dialog)
			Text(format=doubleAirBottom) childCode as Editable Labeled("Code")
			Text(format=doubleAirBottom) childLabel as Editable Labeled("Label")
			Block as Absolute(width=150px) > Button(title="Add", highlight=Outline, size=Small) add

Template(layout=Horizontal) sourceTermTagTemplate
	Text(format=airRight) name as Editable(placeholder="Name")
	Text value as Editable(placeholder="Value")
	MaterialIconButton(title="Remove", icon="Clear") delete as Action